set shell := ["/opt/homebrew/bin/fish", "-c"]
set export

USER_ID := `id -u`

install: 
    brew install koekeishiya/formulae/yabai --HEAD
#need to install dev version of yabai M1 support not yet released

uninstall: 
    brew uninstall koekeishiya/formulae/yabai

upgrade: 
    brew upgrade koekeishiya/formulae/yabai

link:
    -rm ~/Library/LaunchAgents/yabai.plist
    ln -s (pwd)/yabai.plist ~/Library/LaunchAgents/yabai.plist

get-sudoers-entry:
    #!/usr/bin/env bash
    set -euxo pipefail
    USER=$(whoami)
    YABAI_PATH=$(which yabai)
    HASH=$(shasum -a 256 $YABAI_PATH)
    echo "$USER ALL=(root) NOPASSWD: sha256:$HASH $YABAI_PATH --load-sa"

codesign:
    codesign -fs 'yabai-cert' $(which yabai)

link-scripts:
    #!/usr/bin/env fish
    set source_dir ~/.config/yabai/scripts/bin
    set target_dir ~/.local/bin

    for file in $source_dir/*
        set filename (basename $file)
        ln -s $file $target_dir/$filename
    end

start: 
    launchctl load -w ~/Library/LaunchAgents/yabai.plist

stop:
    launchctl unload -w ~/Library/LaunchAgents/yabai.plist

restart:
    launchctl kickstart -k gui/$USER_ID/yabai

status: 
    launchctl list | grep yabai

fix-failed-to-connect-to-socket:
    rm /tmp/yabai_${USER}.socket
    rm /tmp/yabai-sa_${USER}.socket
